plugins {
    id 'checkstyle'
    id 'org.jenkins-ci.jpi' version '0.28.1'
    id 'groovy'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'org.jenkins-ci.plugins'
if (!project.hasProperty('SVNVERSION')) {
    ext.SVNVERSION = '?'
}
version = "${new File('../../make/TICSVERSION').text.trim()}.${SVNVERSION}"

jenkinsPlugin {
    coreVersion = '2.164.3'
    displayName = 'TICS Plugin'
    url = 'http://www.tiobe.com'
    shortName = 'tics'
    configureRepositories = false // don't use the default Jenkins repositories; set to true if this causes trouble outside the TIOBE network

    developers {
        developer {
            id 'dreniers'
            name 'Dennie Reniers'
            email 'dennie.reniers@tiobe.com'
        }
    }
}

repositories {
    maven { 
        url 'http://artifacts.tiobe.com/repository/maven/'
    }
}

dependencies {
    jenkinsPlugins group: 'org.jenkins-ci.plugins', name: 'structs', version: '1.20'
    jenkinsPlugins group: 'org.jenkins-ci.plugins', name: 'cloudbees-folder', version: '6.9'
    jenkinsPlugins group: 'org.jenkins-ci.plugins', name: 'credentials', version: '2.3.0'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', 'version': '4.3.6'
    implementation group: 'com.google.code.gson', name: 'gson', 'version': '2.3.1'
    implementation group: 'joda-time', name: 'joda-time', 'version': '2.3'
    compile group: 'com.google.guava', name: 'guava', version: '27.0.1-jre'
}

task configureWrapper {
  // Make sure that when upgrading the Gradle wrapper, we still use the local TIOBE mirror.
  doLast {
    wrapper.distributionUrl = "https://artifacts.tiobe.com/repository/gradle-distributions/gradle-${wrapper.gradleVersion}-bin.zip"
  }
}

wrapper.dependsOn configureWrapper
